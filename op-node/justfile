import '../just/go.just'

# Build ldflags string
_LDFLAGSSTRING := "'" + trim(
    "-X main.GitCommit=" + GITCOMMIT + " " + \
    "-X main.GitDate=" + GITDATE + " " + \
    "-X github.com/ethereum-optimism/optimism/op-node/version.Version=" + VERSION + " " + \
    "-X github.com/ethereum-optimism/optimism/op-node/version.Meta=" + VERSION_META + " " + \
    "") + "'"

BINARY := "./bin/op-node"

# Build op-node binary
op-node: (go_build BINARY "./cmd" "-ldflags" _LDFLAGSSTRING)

# Clean build artifacts
clean:
    rm -f {{BINARY}}

# Run tests
test: (go_test "./...")

# Generate mocks
generate-mocks: (go_generate "./...")

# Update readme
readme:
    doctoc README.md

[private]
node_fuzz_task FUZZ TIME='10s': (go_fuzz FUZZ TIME "./rollup/derive")

# Run fuzz tests
fuzz:
	printf "%s\n" \
		"just node_fuzz_task FuzzL1InfoBedrockRoundTrip" \
		"just node_fuzz_task FuzzL1InfoEcotoneRoundTrip" \
		"just node_fuzz_task FuzzL1InfoAgainstContract" \
		"just node_fuzz_task FuzzUnmarshallLogEvent" \
		"just node_fuzz_task FuzzParseFrames" \
		"just node_fuzz_task FuzzFrameUnmarshalBinary" \
		"just node_fuzz_task FuzzBatchRoundTrip" \
		"just node_fuzz_task FuzzDeriveDepositsRoundTrip" \
		"just node_fuzz_task FuzzDeriveDepositsBadVersion" \
		"just node_fuzz_task FuzzParseL1InfoDepositTxDataValid" \
		"just node_fuzz_task FuzzParseL1InfoDepositTxDataBadLength" \
		"just node_fuzz_task FuzzRejectCreateBlockBadTimestamp" \
		"just node_fuzz_task FuzzDecodeDepositTxDataToL1Info" \
	| parallel -j 8 {}